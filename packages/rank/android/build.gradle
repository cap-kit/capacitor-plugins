buildscript {
    ext.kotlin_version = project.hasProperty("kotlin_version") ? rootProject.ext.kotlin_version : '2.2.20'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:9.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jlleitschuh.gradle.ktlint" version "14.0.1" apply false
}

ext {
    junitVersion = project.hasProperty('junitVersion') ? rootProject.ext.junitVersion : '4.13.2'
    androidxAppCompatVersion = project.hasProperty('androidxAppCompatVersion') ? rootProject.ext.androidxAppCompatVersion : '1.7.1'
    androidxJunitVersion = project.hasProperty('androidxJunitVersion') ? rootProject.ext.androidxJunitVersion : '1.3.0'
    androidxEspressoCoreVersion = project.hasProperty('androidxEspressoCoreVersion') ? rootProject.ext.androidxEspressoCoreVersion : '3.7.0'
    androidxCoreKTXVersion = project.hasProperty('androidxCoreKTXVersion') ? rootProject.ext.androidxCoreKTXVersion : '1.17.0'
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'org.jlleitschuh.gradle.ktlint'

import groovy.json.JsonSlurper

def getPluginVersion() {
    try {
        def packageJsonFile = file('../package.json')
        if (packageJsonFile.exists()) {
            def packageJson = new JsonSlurper().parseText(packageJsonFile.text)
            if (packageJson.version) {
                return packageJson.version
            }
        }
    } catch (Exception e) {
        throw new GradleException("Failed to read plugin version from package.json", e)
    }

    throw new GradleException("Plugin version not found in ../package.json")
}

def pluginVersion = getPluginVersion()

android {
    namespace = "io.capkit.rank"
    compileSdk = project.hasProperty('compileSdkVersion') ? rootProject.ext.compileSdkVersion as Integer : 36

    // AGP 8.0+ disables BuildConfig by default for libraries.
    // We need to enable it to inject the plugin version.
    buildFeatures {
        buildConfig = true
    }

    defaultConfig {
        minSdkVersion project.hasProperty('minSdkVersion') ? rootProject.ext.minSdkVersion as Integer : 24
        targetSdkVersion project.hasProperty('targetSdkVersion') ? rootProject.ext.targetSdkVersion as Integer : 36
        versionCode 1

        // Dynamic versioning (feature enabled)
        versionName = pluginVersion

        // Injects the version into the BuildConfig class ONLY if feature is enabled
        buildConfigField "String", "PLUGIN_VERSION", "\"${pluginVersion}\""

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled = false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lint {
        abortOnError = false
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':capacitor-android')

    // Google Play Review Library
    implementation "com.google.android.play:review:2.0.2"
    // Kotlin extension for ReviewManager
    implementation "com.google.android.play:review-ktx:2.0.2"

    implementation "androidx.appcompat:appcompat:$androidxAppCompatVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "androidx.core:core-ktx:$androidxCoreKTXVersion"

    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "androidx.test.ext:junit:$androidxJunitVersion"
    androidTestImplementation "androidx.test.espresso:espresso-core:$androidxEspressoCoreVersion"
}
